mod IMP-CONFIGURATIONS-BIGSTEP is including IMP-DESUGARED-SYNTAX + STATE .
  sort Configuration .
  op <_,_> : AExp State -> Configuration .
  op   <_> : Int -> Configuration .
  op <_,_> : BExp State -> Configuration .
  op   <_> : Bool -> Configuration .
  op <_,_> : Stmt State -> Configuration .
  op   <_> : State -> Configuration .
  op   <_> : Pgm -> Configuration .
endm

mod IMP-SEMANTICS-BIGSTEP is including IMP-CONFIGURATIONS-BIGSTEP .
  var X : Id .  var Sigma Sigma' Sigma1 Sigma2 : State .
  var I I1 I2 : Int .  var T : Bool .
  var A A1 A2 : AExp .  var B B1 B2 : BExp .  var S S1 S2 : Stmt .  var P : Pgm .

  rl < I,Sigma > => < I > .
 crl < X,Sigma > => < Sigma(X) >
  if Sigma(X) =/=Bool undefined .
 crl < A1 + A2,Sigma > => < I1 +Int I2 > 
  if < A1,Sigma > => < I1 > /\ < A2,Sigma > => < I2 > .
 crl < A1 / A2,Sigma > => < I1 /Int I2 > 
  if < A1,Sigma > => < I1 > /\ < A2,Sigma > => < I2 > /\ I2 =/=Bool 0 .

  rl < T,Sigma > => < T > .
 crl < A1 <= A2,Sigma > => < I1 <=Int I2 > 
  if < A1,Sigma > => < I1 > /\ < A2,Sigma > => < I2 > .
 crl < ! B,Sigma > => < false > 
  if < B,Sigma > => <  true > .
 crl < ! B,Sigma > => < true > 
  if < B,Sigma > => < false > .
 crl < B1 && B2,Sigma > => < false > 
  if < B1,Sigma > => < false > .
 crl < B1 && B2,Sigma > => < T > 
  if < B1,Sigma > => < true > /\ < B2,Sigma > => < T > .
  rl < {},Sigma > => < Sigma > .
 crl < {S},Sigma > => < Sigma' >
  if < S,Sigma > => < Sigma' > .
 crl < X = A ;,Sigma > => < Sigma[I / X] >
  if < A,Sigma > => < I > /\ Sigma(X) =/=Bool undefined .
 crl < S1 S2,Sigma > => < Sigma2 > 
  if < S1,Sigma > => < Sigma1 > /\ < S2,Sigma1 > => < Sigma2 > .
 crl < if (B) S1 else S2,Sigma > => < Sigma1 > 
  if < B,Sigma > => < true > /\ < S1,Sigma > => < Sigma1 > .
 crl < if (B) S1 else S2,Sigma > => < Sigma2 > 
  if < B,Sigma > => < false > /\ < S2,Sigma > => < Sigma2 > .
 crl < while (B) S,Sigma > => < Sigma >
  if < B,Sigma > => < false > .
 crl < while (B) S,Sigma > => < Sigma' > 
  if < B,Sigma > => < true > /\ < S while (B) S,Sigma > => < Sigma' > .

*** <new> The global variable declaration rule is unnecessary now:
--- crl < int Xl ; S > => < Sigma >
---  if < S,(Xl |-> 0) > => < Sigma > .
*** </new>

*** <new> Add the new big-step SOS for local variables
 crl < let X = A in S,Sigma > => < Sigma'[Sigma(X) / X] >
  if < A,Sigma > => < I > /\ < S,Sigma[I / X] > => < Sigma' > .
*** </new>

*** <new> Programs are regarded as statements in the initial state.
*** However, without any option to output, the resulting configuration
*** will always contain only an empty state and nothing else (empty indeed,
*** because all the locally declared variables are removed from the state
*** when their block ends).
 crl < P > => < Sigma >
  if < P, .State > => < Sigma > .
*** </new>
endm
