mod IMP-SEMANTICS-EVALUATION-CONTEXTS is including IMP-SPLIT-PLUG-EVALUATION-CONTEXTS .
  var X : Id .  var I I1 I2 : Int .  var B B2 : BExp .  var S S1 S2 : Stmt .  var P : Pgm .
  var Sigma : State .  var Cfg Cfg' : Configuration .  var Syn Syn' : Syntax  .  var C : Context .

  crl o Cfg => Cfg' if plug(o split(Cfg)) => Cfg' .           --- generic rule enabling splitting
  crl o C[Syn] => C[Syn'] if C =/=Bool [] /\ o Syn => Syn' .  --- characteristic rule

 crl o < C,Sigma >[X] => < C,Sigma >[Sigma(X)] if Sigma(X) =/=Bool undefined .
  rl o I1 + I2 => I1 +Int I2 .
 crl o I1 / I2 => I1 /Int I2 if I2 =/=Bool 0 .
  rl o I1 <= I2 => I1 <=Int I2 .
  rl o ! true => false .
  rl o ! false => true  .
  rl o true && B2 => B2 .
  rl o false && B2 => false .
  rl o {S} => S .
 crl o < C,Sigma >[X = I ;] => < C,Sigma[I / X] >[{}] if Sigma(X) =/=Bool undefined .
  rl o {} S2 => S2 .
  rl o if (true) S1 else S2 => S1 .
  rl o if (false) S1 else S2 => S2 .
  rl o while (B) S => if (B) {S while (B) S} else {} .

*** <new> The next rule is incorrect now.  See small-step SOS.
---  rl o < int Xl ; S > => < S,(Xl |-> 0) > .
*** </new>

*** <new> Programs are regarded as statements in the initial state
  rl o < P > => < P,.State > .
*** </new>

*** <new> Added the reduction semantics rule for let
  rl o < C, Sigma >[let X = I in S] => < C,Sigma[I / X] >[S  X = Sigma(X) ;] .
*** </new>

endm
