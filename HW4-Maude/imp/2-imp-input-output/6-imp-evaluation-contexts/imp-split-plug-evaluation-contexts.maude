mod IMP-CONFIGURATIONS-EVALUATION-CONTEXTS is including IMP-SYNTAX + STATE .

*** <new> Included the module BUFFER as well
  including BUFFER .   
*** </new>

  sorts Configuration ExtendedConfiguration .
  subsort Configuration < ExtendedConfiguration .

*** <new> Added the input/output buffers to statement configurations
  op <_,_,_,_> : Stmt State Buffer Buffer -> Configuration .
*** </new>

*** <new> Added the input buffer to program configurations
  op <_,_> : Pgm Buffer -> Configuration .
*** </new>

  ops (o_) (*_) : Configuration -> ExtendedConfiguration [prec 80] .   --- one step
  var Cfg Cfg' : Configuration .
 crl * Cfg => * Cfg' if o Cfg => Cfg' .
endm

mod IMP-SPLIT-PLUG-EVALUATION-CONTEXTS is including IMP-CONFIGURATIONS-EVALUATION-CONTEXTS .
  sorts Syntax Context .  subsorts AExp BExp Stmt Configuration < Syntax .
  op [] : -> Context .  op _[_] : Context Syntax -> [Syntax] [prec 1] .
  ops split plug : Syntax -> Syntax . --- to split Syntax into context[redex]

  var X : Id .  var A A1 A2 : AExp .  var B B1 B2 : BExp .  var S S1 S2 : Stmt .
  var Sigma : State .  var I1 : Int .  var Syn : Syntax .  var C : Context .

  rl split(Syn) => [][Syn] .  eq plug([][Syn]) = Syn .  eq plug(Syn) = Syn .   --- start/stop contextual representation

*** <new> Modified the context construct, plug equation and split rule below
*** to account for the input/output as well
  var OmegaIn OmegaOut : Buffer .
  op <_,_,_,_> : Context State Buffer Buffer -> Context .  eq plug(< C, Sigma, OmegaIn, OmegaOut > [Syn]) = < plug(C[Syn]), Sigma, OmegaIn, OmegaOut > .
 crl split(< S, Sigma, OmegaIn, OmegaOut >) => < C, Sigma, OmegaIn, OmegaOut > [Syn] if split(S) => C[Syn] .
*** </new>

  op _+_ : Context AExp -> Context .  eq plug((C + A2)[Syn]) = plug(C[Syn]) + A2 .
 crl split(A1 + A2) => (C + A2)[Syn] if split(A1) => C[Syn] .
  op _+_ : AExp Context -> Context .  eq plug((A1 + C)[Syn]) = A1 + plug(C[Syn]) .
 crl split(A1 + A2) => (A1 + C)[Syn] if split(A2) => C[Syn] .

  op _/_ : Context AExp -> Context .  eq plug((C / A2)[Syn]) = plug(C[Syn]) / A2 .
 crl split(A1 / A2) => (C / A2)[Syn] if split(A1) => C[Syn] .
  op _/_ : AExp Context -> Context .  eq plug((A1 / C)[Syn]) = A1 / plug(C[Syn]) .
 crl split(A1 / A2) => (A1 / C)[Syn] if split(A2) => C[Syn] .

  op _<=_ : Context AExp -> Context .  eq plug((C <= A2)[Syn]) = plug(C[Syn]) <= A2 .
 crl split(A1 <= A2) => (C <= A2)[Syn] if split(A1) => C[Syn] .
  op _<=_ : Int Context -> Context .  eq plug((I1 <= C)[Syn]) = I1 <= plug(C[Syn]) .
 crl split(I1 <= A2) => (I1 <= C)[Syn] if split(A2) => C[Syn] .

  op !_ : Context -> Context .  eq plug((! C)[Syn]) = ! plug(C[Syn]) .
 crl split(! B) => (! C)[Syn] if split(B) => C[Syn] .

  op _&&_ : Context BExp -> Context .  eq plug((C && B2)[Syn]) = plug(C[Syn]) && B2 .
 crl split(B1 && B2) => (C && B2)[Syn] if split(B1) => C[Syn] .

  op _=_; : Id Context -> Context .  eq plug((X = C ;)[Syn]) = X = plug(C[Syn]) ; .
 crl split(X = A ;) => (X = C ;)[Syn] if split(A) => C[Syn] .

  op __ : Context Stmt -> Context .  eq plug((C S2)[Syn]) = plug(C[Syn]) S2 .
 crl split(S1 S2) => (C S2)[Syn] if split(S1) => C[Syn] .

  op if(_)_else_ : Context Stmt Stmt -> Context .
 crl split(if (B) S1 else S2) => (if (C) S1 else S2)[Syn] if split(B) => C[Syn] .
  eq plug((if (C) S1 else S2)[Syn]) = if (plug(C[Syn])) S1 else S2 .

*** <new> Add the expected splitting/plugging for print (it is strict)
  op print(_); : Context -> Context .
 crl split(print(A);) => (print(C);)[Syn] if split(A) => C[Syn] .
  eq plug((print(C);)[Syn]) = print(plug(C[Syn])); .
*** </new>

endm
